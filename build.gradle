plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '3.2.7'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.diffplug.spotless' version '6.25.0'
}

allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	group = 'com.nbe2'
	version = '0.0.1'
	sourceCompatibility = '17'

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	test() {
		useJUnitPlatform()
	}

	spotless {
		java {
			googleJavaFormat().aosp()
			importOrder('java', 'javax', 'jakarta', 'org', 'lombok', 'com')
			removeUnusedImports()
			trimTrailingWhitespace()
			endWithNewline()
		}
	}
}


tasks.register("addGitPreCommitHook", Copy) {
	from 'script/pre-commit'
	into '.git/hooks'
}

bootJar {
	enabled = false
}

project(':ea-application') {
	bootJar {
		enabled = false
	}

	jar {
		enabled = false
	}
}







